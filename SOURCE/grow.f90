!======================================================================!
SUBROUTINE grow
!----------------------------------------------------------------------!
! Update an individual tree.
!----------------------------------------------------------------------!
USE CONSTANTS
USE CONTROL
USE TREE
!----------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------!

DO KI = 1, NIND

!----------------------------------------------------------------------!
fAPAR = 1.0 - EXP (-0.5 * LAI)  ! Fraction of PAR absorbed    (fraction)
GPP   = 10.0e-6 * fAPAR   ! Crown gross photosynthesis      (umol/m^2/s)
Resp  = 0.5 * GPP ! Maintenance respiration                 (umol/m^2/s)
Cup = KGCPERMOL * Acrown * (GPP - Resp) ! Net tree C uptake (kgC/tree/s)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
tau = 100.0 * FLOAT (EDPERY * SPERED) ! Structural C residence time  (s)
Clit = Cv (KI) / tau  ! Structural litter flux               (kgC/tree/s)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
dCV = Cup - Clit ! Structural carbon time derivative        (kgC/tree/s)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
Cv (KI) = Cv (KI) + DTTR * dCv
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
V = Cv (KI) / SIGC ! Stem volume                                   (m^3)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
! Update variables                                                   (m)
!----------------------------------------------------------------------!
r = (V / (( FORMF / 3.0) * PI * alpha * 2.0 ** beta)) &
&   ** (1.0 / (2.0 + beta))       ! Stem radius                      (m)
Asapwood = PI * r ** 2 - Aheart (KI) ! Sapwood area                (m^2)
D = 2.0 * r                       ! Stem diameter                    (m)
H (KI) = alpha * r ** beta        ! Stem height                      (m)
Dcrown = a_cd + b_cd * D          ! Crown diameter                   (m)
Acrown = PI * (Dcrown / 2.0) ** 2 ! Crown area                     (m^2)
Acrown = MIN (Aplot,Acrown)
Afoliage (KI) = FASA * Asapwood   ! Foliage area                   (m^2)
LAI = Afoliage (KI) / (Acrown + EPS) ! Leaf area index         (m^2/m^2)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
! Accumulate annual diagnostics.
!----------------------------------------------------------------------!
NPP_ann_acc = NPP_ann_acc + DTTR * Cup / (Aplot + EPS)
!----------------------------------------------------------------------!

END DO ! KI = 1, NIND

!----------------------------------------------------------------------!
END SUBROUTINE grow
!======================================================================!
