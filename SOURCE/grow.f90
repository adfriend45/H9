!======================================================================!
SUBROUTINE grow
!----------------------------------------------------------------------!
! Update an individual tree.
!----------------------------------------------------------------------!
USE CONSTANTS
USE CONTROL
USE TREE
!----------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------!

DO KI = 1, NIND

!----------------------------------------------------------------------!
GPP   = 10.0e-6 * fPAR (KI) ! Crown gross photosynthesis    (umol/m^2/s)
Resp  = 0.5 * GPP ! Maintenance respiration                 (umol/m^2/s)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
! Net tree C uptake (kgC/tree/s).
!----------------------------------------------------------------------!
Cup = KGCPERMOL * Acrown (KI) * (GPP - Resp)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
tau = 100.0 * FLOAT (EDPERY * SPERED) ! Structural C residence time  (s)
Clit = Cv (KI) / tau  ! Structural litter flux               (kgC/tree/s)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
dCV = Cup - Clit ! Structural carbon time derivative        (kgC/tree/s)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
Cv (KI) = Cv (KI) + DTTR * dCv
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
V = Cv (KI) / SIGC ! Stem volume                                   (m^3)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
! Update variables                                                   (m)
!----------------------------------------------------------------------!
! Stem radius                                                         (m)
r = (V / (( FORMF / 3.0) * PI * alpha)) ** (1.0 / (2.0 + beta))
Asapwood = PI * r ** 2 - Aheart (KI) ! Sapwood area                (m^2)
D = 2.0 * r                       ! Stem diameter                    (m)
H (KI) = alpha * r ** beta        ! Stem height                      (m)
Dcrown = a_cd + b_cd * D          ! Crown diameter                   (m)
Acrown (KI) = PI * (Dcrown / 2.0) ** 2 ! Crown area                (m^2)
Acrown (KI) = MIN (Aplot,Acrown(KI))
Afoliage (KI) = FASA * Asapwood   ! Foliage area                   (m^2)
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
! Accumulate annual diagnostics.
!----------------------------------------------------------------------!
NPP_ann_acc = NPP_ann_acc + DTTR * Cup / (Aplot + EPS)
!NPP_ann_acc = NPP_ann_acc + DTTR * Cup / (Acrown (1) + EPS)
!----------------------------------------------------------------------!

END DO ! KI = 1, NIND

!----------------------------------------------------------------------!
END SUBROUTINE grow
!======================================================================!
